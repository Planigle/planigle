<?xml version="1.0" encoding="utf-8"?>
<mx:Button xmlns:mx="http://www.adobe.com/2006/mxml" icon="{parentApplication.imgImport}" creationComplete="init()" click="importFile(event)" toolTip="Import">
	<mx:Script>
		<![CDATA[
			import flash.net.URLRequest;
			import flash.events.DataEvent;
			import flash.events.IOErrorEvent;
			import flash.events.SecurityErrorEvent;
			import flash.events.HTTPStatusEvent;
			import mx.managers.CursorManager;
			import org.planigle.planigle.model.FileUtils;
			import org.planigle.planigle.events.StoryChangedEvent;

			private var fileRef:FileReference;
			private var success:Function;
			private var failure:Function;

			// Initialize my file reference.
			private function init():void
			{
				fileRef = new FileReference();
				fileRef.addEventListener(Event.SELECT, selectComplete);
				fileRef.addEventListener(DataEvent.UPLOAD_COMPLETE_DATA, importSuccessful);
				fileRef.addEventListener(flash.events.HTTPStatusEvent.HTTP_STATUS, importUnsuccessful);
				fileRef.addEventListener(IOErrorEvent.IO_ERROR, importUnsuccessful);
				fileRef.addEventListener(flash.events.Event.CANCEL, importCanceled);
				fileRef.addEventListener(SecurityErrorEvent.SECURITY_ERROR, importUnsuccessful);
            }

			// Set functions to call on success or failure.
			public function onCompletion(success:Function, failure:Function):void
			{
				this.success = success;
				this.failure = failure;
			}

			// Show the browse window.
			private function importFile(event:Event):void
			{
				fileRef.browse();
			}

			// The user has selected a file.
			private function selectComplete(event:Event):void
			{
				fileRef.upload(new URLRequest("../stories/import?_planigle_session_id=" + FileUtils.getSession()));
				CursorManager.setBusyCursor();
			}

			// The file has been uploaded.
			private function importSuccessful(event:DataEvent):void
			{
				CursorManager.removeBusyCursor();
				if(XML(event.data).error.length() > 0)
					failure(XML(event.data).error);
				else
				{
					new StoryChangedEvent().dispatch();
					success(XML(event.data).result);
				}
			}

			// The file has not been uploaded.
			private function importUnsuccessful(event:Event):void
			{
				CursorManager.removeBusyCursor();
				failure("The file could not be imported");
			}

			// The file has been canceled.
			private function importCanceled(event:Event):void
			{
				CursorManager.removeBusyCursor();
			}
		]]>
	</mx:Script>
</mx:Button>