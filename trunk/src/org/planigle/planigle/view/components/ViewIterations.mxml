<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:component="org.planigle.planigle.view.components.*" height="100%" y="0" width="100%" x="0" creationComplete="created(event)">
	<mx:Script>
		<![CDATA[
		import mx.rpc.events.ResultEvent;
		import mx.collections.ArrayCollection;
		import mx.controls.Alert;
		import mx.controls.dataGridClasses.DataGridColumn;
		import mx.events.CloseEvent;
		import mx.binding.utils.ChangeWatcher;
		import org.planigle.planigle.model.ViewModelLocator;
		import org.planigle.planigle.model.IndividualFactory;
		import org.planigle.planigle.model.ReleaseFactory;
		import org.planigle.planigle.model.Release;
		import org.planigle.planigle.model.IterationFactory;
		import org.planigle.planigle.model.Iteration;
		import org.planigle.planigle.model.DateUtils;
		import org.planigle.planigle.view.StoriesTab;
		import org.planigle.planigle.view.ChangesTab;
		import org.planigle.planigle.events.AuditChangedEvent;
		import org.planigle.planigle.events.IterationChangedEvent;

		[Bindable]
		public var release:Release;
		
		[Bindable]
		private var _error:String;

		[Bindable]
		private var viewModelLocator:ViewModelLocator = ViewModelLocator.getInstance();

		[Bindable]
		public var factory:IndividualFactory = IndividualFactory.getInstance();

		[Bindable]
		public var iterations:ArrayCollection = new ArrayCollection();

		// Set up event listeners.  Called when the canvas has been created.
		private function created(event:Event):void
		{
			ChangeWatcher.watch( factory, "currentIndividual", resetState );			
			ChangeWatcher.watch( IterationFactory.getInstance(), "iterations", setIterations );			
			ChangeWatcher.watch( this, "release", setIterations );			
			setIterations(event);
		}
				
		// Update the error message.
		private function setErrorMessage(string:String):void {iterationError.setStyle("color","Red"); _error = string}
		private function setSuccessMessage(string:String):void {iterationError.setStyle("color","Green"); _error = string}
		private function clearErrorMessage():void {_error = ""}

		// Set the iterations to a copy (so that changes here won't affect elsewhere).
		private function setIterations(event:Event):void
		{
			var newIterations:ArrayCollection = new ArrayCollection();
			for each (var iteration:Iteration in IterationFactory.getInstance().iterations)
			{
				if (!release || iteration.isIn(release))
					newIterations.addItem(iteration);
			}
			iterations = newIterations;
		}
		
		// Reset my state to how it was initially.
		public function resetState(event:Event):void
		{
			currentItem = null;
			clearErrorMessage();
			currentState = '';
		}

		// Show user a form to create a resource.
		private function showCreate(event:Event):void
		{
			clearErrorMessage();
			currentState = 'Change';
			iterationFieldName.setFocus();
			
			if(IterationFactory.getInstance().iterations.length > 0)
			{
				var last:Iteration = Iteration(IterationFactory.getInstance().iterations.getItemAt(IterationFactory.getInstance().iterations.length - 1));
				iterationFieldName.text = last.incrementName();
				iterationFieldName.setSelection(0, iterationFieldName.text.length );
				iterationFieldStart.selectedDate = release ? (release.start.getTime() > last.finish.getTime() ? release.start : last.finish) : last.finish;
				iterationFieldLength.text = last.length.toString();
			}
			else
			{
	 			iterationFieldName.text = 'Iteration 1';
				iterationFieldStart.selectedDate = release ? release.start : new Date();
				iterationFieldLength.text = '2';
			}
			iterationFieldRetrospectiveResults.text = '';
			iterationBtnChange.label = 'Create';
			iterationBtnInfo.visible = false;
		}

		private var currentItem:Object;

		// Show user a form to update a resource.
		public function showUpdate(event:Event) : void
		{
			currentItem = iterationResourceGrid.selectedItem;
			if (currentItem)
			{
				clearErrorMessage();
				currentState = 'Change';
				iterationFieldName.text = currentItem.name;
				iterationFieldName.setFocus();
				iterationFieldName.setSelection(0, iterationFieldName.text.length );
				iterationFieldStart.selectedDate = currentItem.start;
				iterationFieldLength.text = currentItem.length;
				iterationFieldRetrospectiveResults.text = currentItem.retrospectiveResults;
				iterationBtnChange.label = 'Update';
				iterationBtnInfo.visible = true;
			}
		}

		// Confirm that user wants to delete the resource.
		public function showDelete(event:Event) : void
		{
			Alert.show("Are you sure you want to delete '" + iterationResourceGrid.selectedItem.name + "'?", "Delete", 3, this,
			function(event:CloseEvent):void
			{
				if (event.detail==Alert.YES)
				{
					clearErrorMessage();
					deleteResource(iterationResourceGrid.selectedItem);
				}
			});
		}
		
		// Create parameters for the user's request to the server.
		private function getParams() : Object
		{
			var params:Object = new Object();
			params['record[project_id]'] = factory.currentIndividual.projectId;
			params['record[name]'] = iterationFieldName.text;
			params['record[start]'] = iterationFieldStart.selectedDate;
			params['record[length]'] = iterationFieldLength.text;
			params['record[retrospective_results]'] = iterationFieldRetrospectiveResults.text;
			params['random'] = Math.random(); // Prevents caching
			return params;
		}

		// Tell the server to change a resource.
		private function changeResource(event:Event) : void
		{
			if ( iterationBtnChange.label == 'Create' )
				createResource(event);
			else
				updateResource(event);
		}
		
		// Tell the server to create a resource.
		private function createResource(event:Event) : void 
		{
			IterationFactory.getInstance().createIteration(getParams(), handleCreateSuccess, handleFailure);
		}
		
		// Tell the server to update a resource.
		private function updateResource(event:Event) : void
		{
			currentItem.update(getParams(), handleSuccess, handleFailure);
		}

		// Tell the server to delete a resource.
		private function deleteResource(resource:Object):void
		{
			resource.destroy(handleSuccess, handleFailure);
		}
		
		// Handle a successful create.
		private function handleCreateSuccess(item:Object):void
		{
			showCreate(null);
			setSuccessMessage("Iteration was successfully created." + ((release == null || item.isIn(release)) ? "" : "  Iteration does not show in the list since it is not in the currently selected release."));
		}

		// Handle a successful change.
		private function handleSuccess():void
		{
			resetState(null);
		}

		// Handle errors when making changes.
		private function handleFailure(error:XMLList):void
		{
			setErrorMessage(error);
		}
		
		// Go to iteration planning.
		public function showPlan(event:Event):void
		{
			var stories:StoriesTab = viewModelLocator.stories;
			stories.release.selectedIndex = ReleaseFactory.getInstance().releases.length + 1;
			stories.handleReleaseFilter(null); // ensure filter affect occurs.
			stories.iteration.selectedItem = iterationResourceGrid.selectedItem;
			stories.handleFilter(null); // ensure filter affect occurs.
			viewModelLocator.selectedTab = stories;
		}
		
		// Format the start date into a more readable format.
		private function formatDate(item:Object, column:DataGridColumn):String
		{
			return DateUtils.formatDate(item.start);
		}
		
		// Show the history for the current object.
		public function showHistory(event:Event) : void
		{
			new AuditChangedEvent(null, 'Iteration', null, null, String(currentItem.id)).dispatch();
			viewModelLocator.selectedTab = viewModelLocator.changes;
		}
		]]>
	</mx:Script>
 	
	<mx:states>
		<mx:State name="Change">
			<mx:RemoveChild target="{iterationBtnCreate}"/>
			<mx:AddChild relativeTo="{iterationResourceGrid}" position="before">
				<mx:target>
					<mx:Form id="iterationChangeForm" defaultButton="{iterationBtnChange}" paddingTop="0" paddingBottom="0" verticalGap="5">
						<mx:FormItem label='Iteration Name' required="true">
							<mx:TextInput id='iterationFieldName' maxChars="40" text='' width="350"/>
						</mx:FormItem>
						<mx:FormItem label='Start' required="true">
							<mx:DateField id='iterationFieldStart' editable="true" showToday="true" width="100"/>
						</mx:FormItem>
						<mx:FormItem label='Length (in weeks)' required="true">
							<mx:TextInput id='iterationFieldLength' restrict="0-9" text='' width="100"/>
						</mx:FormItem>
						<mx:FormItem label='Retrospective Results'>
							<component:HTMLTextArea id='iterationFieldRetrospectiveResults'/>
						</mx:FormItem>
						<mx:ControlBar width="100%">
							<mx:Spacer width="50"/>
							<mx:Button label="Change" id="iterationBtnChange" click="changeResource(event)"/>
							<mx:LinkButton label="Cancel" id="iterationBtnCancel" click="resetState(event)"/>
							<mx:LinkButton id="iterationBtnInfo" icon="{parentApplication.imgInfo}" toolTip="History" click="showHistory(event);"/>
						</mx:ControlBar>
					</mx:Form>	
				</mx:target>
			</mx:AddChild>
		</mx:State>
	</mx:states>
	
	<mx:transitions>
		<mx:Transition id="iterationChangeItem" fromState="*" toState="Change">
			<mx:Sequence targets="{[iterationCombinedPanel]}">
				<mx:RemoveChildAction/>
				<mx:Resize target="{iterationCombinedPanel}"/>
				<mx:AddChildAction/>
			</mx:Sequence>
		</mx:Transition>

		<mx:Transition id="iterationReturnList" fromState="Change" toState="*">
			<mx:Sequence targets="{[iterationCombinedPanel]}">
				<mx:RemoveChildAction/>
				<mx:Resize target="{iterationCombinedPanel}"/>
				<mx:AddChildAction/>
			</mx:Sequence>
		</mx:Transition>

	</mx:transitions>

	<mx:Panel id="iterationCombinedPanel" height="100%" width="100%" borderThicknessTop="0" borderThicknessLeft="0" borderThicknessRight="0" headerHeight="0">
		<mx:HBox width="100%" horizontalAlign="right" horizontalGap="8" verticalAlign="middle" id="iterationHbox" paddingTop="5" paddingLeft="5" paddingRight="5">
			<mx:Label text="Iterations" fontWeight="bold" textDecoration="underline"/>
			<mx:Text id="iterationError" styleName="error" htmlText="{_error}" width="100%"/>
			<mx:Button icon="{parentApplication.imgAdd}" toolTip="Create" id="iterationBtnCreate" click="showCreate(event);" visible="{factory.currentIndividual.isAtLeastProjectAdmin()}"/>
		</mx:HBox>
		<mx:DataGrid xmlns:mx="http://www.adobe.com/2006/mxml" id="iterationResourceGrid" dataProvider="{iterations}" rowCount="{IterationFactory.getInstance().iterations.length}" width="100%" height="100%" doubleClickEnabled="{factory.currentIndividual.isAtLeastProjectAdmin()}" doubleClick="showUpdate(event)">
			<mx:columns>
				<mx:DataGridColumn headerText="Name" dataField="name"/>
				<mx:DataGridColumn headerText="Start" width="120" dataField="start" labelFunction="formatDate" />
				<mx:DataGridColumn headerText="Length (in weeks)" width="80" dataField="length"/>
				<mx:DataGridColumn editable="false" width="140" minWidth="140" sortable="false">
					<mx:itemRenderer>
						<mx:Component>
							<mx:Box direction="horizontal" horizontalGap="0">
								<mx:LinkButton id="iterationBtnPlan" icon="{parentApplication.imgPlan}" toolTip="Plan" click="outerDocument.showPlan(event);"/>
								<mx:LinkButton id="iterationBtnEdit" icon="{parentApplication.imgEdit}" toolTip="Edit" click="outerDocument.showUpdate(event);" visible="{outerDocument.factory.currentIndividual.isAtLeastProjectAdmin()}"/>
								<mx:LinkButton id="iterationBtnDelete" icon="{parentApplication.imgCross}" toolTip="Delete" click="outerDocument.showDelete(event);" visible="{outerDocument.factory.currentIndividual.isAtLeastProjectAdmin()}"/>
							</mx:Box>
						</mx:Component>
					</mx:itemRenderer>
				</mx:DataGridColumn>
			</mx:columns>
		</mx:DataGrid>
	</mx:Panel>
</mx:Canvas>