package org.planigle.planigle.commands
{
	import com.adobe.cairngorm.commands.ICommand;
	import com.adobe.cairngorm.control.CairngormEvent;
	
	import mx.controls.Alert;
	import mx.rpc.IResponder;
	
	import org.planigle.planigle.business.IterationsDelegate;
	import org.planigle.planigle.model.Iteration;
	
	public class DeleteIterationCommand implements ICommand, IResponder
	{
		private var iteration:Iteration;
		private var notifySuccess:Function;
		private var notifyFailure:Function;
		
		public function DeleteIterationCommand(anIteration:Iteration, aSuccessFunction:Function, aFailureFunction:Function)
		{
			iteration = anIteration;
			notifySuccess = aSuccessFunction;
			notifyFailure = aFailureFunction;
		}
		
		// Required for the ICommand interface.  Event must be of type Cairngorm event.
		public function execute(event:CairngormEvent):void
		{
			new IterationsDelegate( this ).deleteIteration(iteration);
		}
		
		// Handle successful server request.
		public function result( event:Object ):void
		{
			var result:XML = XML(event.result);
			if (result.error.length() > 0)
			{
				if (notifyFailure != null)
					notifyFailure(result.error);
			}
			else
			{
				iteration.destroyCompleted();
				if (notifySuccess != null)
					notifySuccess();
			}
		}
		
		// Handle case where error occurs.
		public function fault( event:Object ):void
		{
			Alert.show(event.fault.faultString);
		}
	}
}