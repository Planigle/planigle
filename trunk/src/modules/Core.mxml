<?xml version="1.0" encoding="utf-8"?>
<mx:Module width="100%" height="100%" xmlns:mx="http://www.adobe.com/2006/mxml" paddingBottom="0" paddingTop="0" paddingLeft="0" paddingRight="0"
	layout="absolute"
	xmlns:view="org.planigle.planigle.view.*" initialize="onInitialize(event)">
	
	<mx:Script>
		<![CDATA[
			import com.adobe.viewsource.ViewSource;
			import flash.display.DisplayObject;
			import mx.binding.utils.ChangeWatcher;
			import mx.binding.utils.BindingUtils;
			import mx.managers.CursorManager;
			import mx.events.ModuleEvent;
        	import mx.modules.ModuleManager;
        	import mx.modules.IModuleInfo;
			import org.planigle.planigle.model.IndividualFactory;
			import org.planigle.planigle.model.TabModelLocator;
			import org.planigle.planigle.model.ViewModelLocator;
			import org.planigle.planigle.events.CompanyChangedEvent;
			import org.planigle.planigle.events.IndividualChangedEvent;
			import org.planigle.planigle.events.ReleaseChangedEvent;
			import org.planigle.planigle.events.IterationChangedEvent;
			import org.planigle.planigle.events.StoryChangedEvent;
			import org.planigle.planigle.events.LogoutEvent;
			import org.planigle.planigle.events.URLEvent;
			import org.planigle.planigle.view.ScheduleTab;
			import org.planigle.planigle.view.StoriesTab;
			import org.planigle.planigle.view.PeopleTab;
			import org.planigle.planigle.view.ChangesTab;
			import org.planigle.planigle.view.SystemTab;

			[Bindable]
			private var factory:IndividualFactory = IndividualFactory.getInstance();

			private var schedule:ScheduleTab = new ScheduleTab();
			private var stories:StoriesTab = new StoriesTab();
			private var people:PeopleTab = new PeopleTab();
			private var changes:ChangesTab = new ChangesTab();
			private var system:SystemTab = new SystemTab();

			// Add the base tabs.
			private function onInitialize(event:Event):void
			{
				CursorManager.setBusyCursor();
				TabModelLocator.getInstance().stories = stories;
				TabModelLocator.getInstance().changes = changes;
				mainViewStack.addChild(people);
				ChangeWatcher.watch( factory, "currentIndividual", changeIndividual );
				ChangeWatcher.watch( factory, "individuals", updateProjects );
				changeIndividual(event); // This is to catch it the first time (The event has already been sent).
				TabModelLocator.getInstance().selectedTab = mainViewStack.getChildAt(0) == stories ? stories : people;
				BindingUtils.bindProperty(mainViewStack, "selectedChild", TabModelLocator.getInstance(), "selectedTab");
				CursorManager.removeBusyCursor();
			}

			private var watcher:ChangeWatcher;
			private var watcher2:ChangeWatcher;

			// The user has changed.
			private function changeIndividual(event:Event):void
			{
				if (watcher)
					watcher.unwatch();

				watcher = factory.currentIndividual ? ChangeWatcher.watch( factory.currentIndividual, "projectId", changeProject ) : null;
				watcher2 = factory.currentIndividual && factory.currentIndividual.selectedProject ? ChangeWatcher.watch( factory.currentIndividual.selectedProject, "premiumExpiry", resetState ) : null;
				resetState(event);
			}

			// Update the selected projects pullsdon.
			private function updateProjects(event:Event):void
			{
				IndividualFactory.current().allProjectsChanged();
				selectedProject.selectedItem = IndividualFactory.current().selectedProject;
			}

			// The project has changed.
			private function changeProject(event:Event):void
			{
				if (watcher2)
					watcher2.unwatch();
				watcher2 = factory.currentIndividual.selectedProject ? ChangeWatcher.watch( factory.currentIndividual.selectedProject, "premiumExpiry", resetState ) : null;
				resetState(event);
				refresh();
			}

			// Hide the specified tab.
			private function hideTab(tab:DisplayObject):void
			{
				if (mainViewStack.getChildren().indexOf(tab) != -1 )
					mainViewStack.removeChild( tab );
			}
			
			// Show the specified tab.
			private function showTab(tab:DisplayObject, location:int):void
			{
				if (mainViewStack.getChildren().indexOf(tab) == -1 )
					mainViewStack.addChildAt( tab, location );
			}

			// Reset based on a new user.
			private function resetState(event:Event):void
			{
				if (factory.currentIndividual)
				{
					if (factory.currentIndividual.isAdmin())
						showTab(system, mainViewStack.getChildren().length);
					else
						hideTab(system);

					if (factory.currentIndividual.isAdminOnly())
					{
						hideTab(changes);
						hideTab(stories);
						hidePremium();
						hideTab(schedule);
					}
					else
					{
						showTab(changes, 1);
						showTab(schedule, 0);
						showTab(stories, 0);
						factory.currentIndividual.isPremium() ? showPremium() : hidePremium();
					}
				}
			}

			// Reset the state of the selected tab.
			private function resetTab(event:Event):void
			{
				Object(mainViewStack.selectedChild).resetState(event);
			}	
					
			// Refresh any cached data.
			private function refresh():void
			{
				new CompanyChangedEvent().dispatch();
				new IndividualChangedEvent().dispatch();
				if (!factory.currentIndividual.isAdminOnly())
				{
					new ReleaseChangedEvent().dispatch();
					new IterationChangedEvent().dispatch();
					new StoryChangedEvent().dispatch();
				}
			}
			
			// Log out.
			private function logout():void
			{
				new LogoutEvent().dispatch();
			}			

			private var premiumModule:IModuleInfo;
			private var premium:DisplayObject;
			private var isLoading:Boolean = false;

			// Show premium information.
			private function showPremium():void
			{
				if (premium)
					showTab(premium, 1);					
				else if (!isLoading)
					loadPremiumModule();
			}

			// Hide premium information.
			private function hidePremium():void
			{
				if (premium)
					hideTab(premium);
			}

			// Load the premium module.
			private function loadPremiumModule():void
			{
				isLoading = true;
				premiumModule = ModuleManager.getModule("modules/Premium.swf");
				premiumModule.addEventListener(ModuleEvent.READY, moduleEventHandler);
	            premiumModule.load(); 
			}
			
			// The module has finished loading.
			private function moduleEventHandler(event:ModuleEvent):void
			{
				isLoading = false;
				premium = premiumModule.factory.create() as DisplayObject;
				showTab(premium, 1);
			}
			
			// A different child has been selected.
			private function updateChild(event:Event):void
			{
				TabModelLocator.getInstance().selectedTab = mainViewStack.selectedChild;
			}
			
			// The user has changed the project that they are viewing.
			private function selectProject():void
			{
				IndividualFactory.current().update({"record[selected_project_id]": (selectedProject.selectedItem.id ? selectedProject.selectedItem.id : "")}, refresh, null);
			}
		]]>
	</mx:Script>
	<mx:HBox horizontalGap="5" width="100%">
		<mx:TabBar id="mainNavigation" dataProvider="{mainViewStack}" itemClick="resetTab(event)">
		</mx:TabBar>
		<mx:HBox horizontalAlign="right" width="100%">
			<mx:ComboBox id="selectedProject" fillColors="[0xffffff, 0xffffff]" fillAlphas="[1,1]" dataProvider="{IndividualFactory.getInstance().currentIndividual.allProjects}" visible="{IndividualFactory.getInstance().currentIndividual.allProjects.length>1}" change="selectProject()" selectedItem="{IndividualFactory.current().selectedProject}" />
			<mx:LinkButton label="Refresh" styleName="ToolbarLink" id="refreshButton" click="{refresh()}"/>
			<mx:LinkButton label="Log out" styleName="ToolbarLink" id="logoutButton" click="{logout()}"/>
			<mx:LinkButton id="planigleButton" icon="{parentApplication.imgLogoSmall}" toolTip="Planigle Project Page" click="new URLEvent('http://code.google.com/p/planigle/').dispatch()"/>
		</mx:HBox>
	</mx:HBox>
	<mx:ViewStack id="mainViewStack" y="25" width="100%" height="100%" paddingBottom="0" paddingTop="0" paddingLeft="0" paddingRight="0" change="updateChild(event)">
	</mx:ViewStack>
</mx:Module>